buildscript {
    ext {
        kotlinVersion = '1.3.50'
    }
    repositories {
        google()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://firebase.bintray.com/gradle' }
        maven { url 'https://jetbrains.bintray.com/trove4j' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.google.gms:google-services:4.3.1'
        classpath 'io.fabric.tools:gradle:1.31.0'
    }
}

allprojects {
    apply plugin: 'checkstyle'
    ext {
        ankoVersion = "0.10.8"
        androidLibrariesVersion = "1.0.0"
        lifecycleExtensionsVersion = "2.0.0"
        daggerVersion = "2.24"
        mockitoVersion = "3.0.0"
    }
    repositories {
        google()
        mavenCentral()
        maven { url 'https://dl.bintray.com/jetbrains/anko/' }
        maven { url 'https://dl.bintray.com/firebaseui/firebase-ui' }
        maven { url 'https://dl.bintray.com/chthai64/maven' }
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://jetbrains.bintray.com/trove4j' }
    }

//    This will check for the usage of deprecated APIs when building.
//    This will tell exactly which line is calling the deprecated API.
//    It is commented-out because it increases build time.
//    gradle.projectsEvaluated {
//        tasks.withType(JavaCompile) {
//            options.compilerArgs << "-Xlint:deprecation"
//        }
//    }

    checkstyle {
        toolVersion = '8.19'
    }
    task checkstyle(type: Checkstyle) {
        // google_checks.xml from:
        // https://github.com/checkstyle/checkstyle/blob/master/src/main/resources/google_checks.xml
        configFile new File(rootDir, "tools/checkstyle/google_checks.xml")
        configProperties.checkstyleSuppressionPath =
                new File(rootDir, "tools/checkstyle/suppressions.xml").absolutePath
        source 'src'
        include '**/*.java'
        exclude '**/gen/**'
        exclude '**/R.java'
        exclude '**/BuildConfig.java'
        classpath = files()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}