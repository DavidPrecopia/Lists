apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: "de.mannodermaus.android-junit5"

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        consumerProguardFiles 'consumer-rules.pro'
        resConfig rootProject.resConfigs
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    api project(":domain")

    // Cloud Firestore
    implementation "com.google.firebase:firebase-firestore-ktx:$rootProject.firestoreVersion"
    // Firebase Authentication
    api ("com.firebaseui:firebase-ui-auth:$rootProject.firebaseAuthUiVersion") {
        exclude group: "com.google.android.material", module: "material"
        because "This is exposed because I am using it to authenticate users. I should refactor to a custom " +
                "implementation for decoupling sakes, however, this will do for now because that will take time " +
                "I would rather spend elsewhere." +
                "I am excluding the material library so I can continue using an older version of it."
    }
    implementation ("com.google.android.material:material:$rootProject.materialDesignVersion") {
        because "This fixes the Gradle `assemble` command failing. This dependency is required by Firebase UI."
    }
    implementation "com.google.firebase:firebase-auth-ktx:$rootProject.firebaseAuthVersion"
    implementation "com.google.android.gms:play-services-auth:$rootProject.playServicesAuthVersion"

    // RxJava
    implementation "io.reactivex.rxjava3:rxjava:$rootProject.rxJavaVersion"

    // Timber
    debugImplementation "com.jakewharton.timber:timber:$rootProject.timberVersion"

    // Local testing
    testImplementation "org.junit.jupiter:junit-jupiter-api:$rootProject.junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$rootProject.junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$rootProject.junitVersion"
    testImplementation "io.mockk:mockk:$rootProject.mockkVersion"
    testImplementation "org.assertj:assertj-core:$rootProject.assertjVersion"
}
