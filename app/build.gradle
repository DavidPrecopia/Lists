apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

// enables LayoutContainer
androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId "com.example.david.lists"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 13
        versionName "2.0.1"
        // This ensures that ProGuard removes un-supported language resources.
        // https://developer.android.com/studio/build/shrink-code#unused-alt-resources
        resConfig "en"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        releaseOptimized {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            // Prevents Crashlytics from updating app resources with its own unique build ID.
            // This is not needed for the debug build.
            ext.alwaysUpdateBuildId = false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "androidx.core:core-ktx:$coreVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    implementation "androidx.appcompat:appcompat:$coreVersion"

    implementation "com.google.android.material:material:$androidLibrariesVersion"
    implementation "androidx.recyclerview:recyclerview:$androidLibrariesVersion"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Swipe Reveal Layout
    implementation 'com.chauthai.swipereveallayout:swipe-reveal-layout:1.4.1'

    //Anko
    implementation "org.jetbrains.anko:anko-commons:$ankoVersion"

    // Fragment KTX
    implementation "androidx.fragment:fragment-ktx:1.1.0"

    // Firebase
    implementation 'com.google.firebase:firebase-core:17.1.0'
    // Cloud Firestore
    implementation 'com.google.firebase:firebase-firestore-ktx:21.0.0'
    // Firebase Authentication
    implementation 'com.google.firebase:firebase-auth:19.0.0'
    implementation 'com.firebaseui:firebase-ui-auth:5.0.0'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    // Firebase Crashlytics
    // This cannot be in the `releaseImplementation` configuration because Crashlytics
    // is referenced in debug code.
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'

    // Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    // Timber
    debugImplementation 'com.jakewharton.timber:timber:4.7.1'

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.0-beta-3"

    // Local testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-library:2.1'
    testImplementation "org.mockito:mockito-inline:$mockitoVersion"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"

    // Instrumentation testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
}

apply plugin: 'com.google.gms.google-services'
