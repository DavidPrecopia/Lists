apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.example.david.lists"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 5
        versionName "1.3"
        resConfig "en"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        releaseOptimized {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            // DEX's 65k limit has been reached
            // Avoiding MultiDex
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    def androidLibraries = "1.0.0"
    def lifecycleExtensions = "2.0.0"

    implementation "com.google.android.material:material:$androidLibraries"
    implementation "androidx.recyclerview:recyclerview:$androidLibraries"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Swipe Reveal Layout
    implementation 'com.chauthai.swipereveallayout:swipe-reveal-layout:1.4.1'

    // LiveData and ViewModel
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleExtensions"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleExtensions"

    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.6'
    // Cloud Firestore
    implementation 'com.google.firebase:firebase-firestore:17.1.5'
    // Firebase Authentication
    implementation 'com.google.firebase:firebase-auth:16.1.0'
    implementation 'com.firebaseui:firebase-ui-auth:4.3.0'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    // Firebase Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'
    // Firebase Performance Monitoring
    implementation 'com.google.firebase:firebase-perf:16.2.3'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.4'

    // Timber
    debugImplementation 'com.jakewharton.timber:timber:4.7.1'

    // Leak Canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.2'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

apply plugin: 'com.google.gms.google-services'