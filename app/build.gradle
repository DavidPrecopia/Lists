apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'io.fabric'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

// enables LayoutContainer
androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId "com.example.david.lists"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 14
        versionName "2.1"
        // This ensures that ProGuard removes un-supported language resources.
        // https://developer.android.com/studio/build/shrink-code#unused-alt-resources
        resConfig "en"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            // Disables Crashlytics
            ext.enableCrashlytics = false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "androidx.core:core-ktx:$core_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "androidx.appcompat:appcompat:$core_version"
    implementation "androidx.activity:activity-ktx:1.0.0"
    implementation "androidx.fragment:fragment-ktx:$core_version"

    implementation "com.google.android.material:material:$android_libraries_version"
    implementation "androidx.recyclerview:recyclerview:$android_libraries_version"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Navigation
    implementation "androidx.navigation:navigation-runtime-ktx:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Swipe Reveal Layout
    implementation 'com.chauthai.swipereveallayout:swipe-reveal-layout:1.4.1'

    //Anko
    implementation "org.jetbrains.anko:anko-commons:$anko_version"

    // Firebase
    implementation 'com.google.firebase:firebase-core:17.2.0'
    // Cloud Firestore
    implementation 'com.google.firebase:firebase-firestore-ktx:21.1.1'
    // Firebase Authentication
    implementation 'com.google.firebase:firebase-auth:19.0.0'
    implementation 'com.firebaseui:firebase-ui-auth:6.0.2'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    // Firebase Crashlytics
    releaseImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'

    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // Timber
    debugImplementation 'com.jakewharton.timber:timber:4.7.1'

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.0-beta-3"

    // Local testing
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation "org.assertj:assertj-core:3.13.2"

    // Instrumentation testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation "org.assertj:assertj-core:3.13.2"
}

apply plugin: 'com.google.gms.google-services'


// Fixes issue with kapt and Dagger when building from the command line.
// Per, https://github.com/google/dagger/issues/1449
// Issue has been fixed in Kotlin v1.3.60
if (project.hasProperty('kapt')) {
    // Reference for 'kapt' DSL: https://kotlinlang.org/docs/reference/kapt.html#java-compiler-options
    kapt {
        // we expect this closure to run over a org.jetbrains.kotlin.gradle.plugin.KaptExtension
        javacOptions {
            option("-source", "8")
            option("-target", "8")
        }
    }
}